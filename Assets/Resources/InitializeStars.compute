#pragma kernel InitializeStars

#include "Random.cginc"

#define PI 3.14159265358979

struct StarData {
    float4 position;
    float4x4 rotation;
};

RWStructuredBuffer<StarData> _Stars;
float _Radius;

[numthreads(256,1,1)]
void InitializeStars(uint3 id : SV_DispatchThreadID) {
    StarData star;

    // Random spherical coordinates method from: https://karthikkaranth.me/blog/generating-random-points-in-a-sphere/
    float u = randValue(id.x + 12345) * 2.0f - 1.0f;
    float v = randValue(id.x + 54321) * 2.0f - 1.0f;
    float theta = u * 2.0f * PI;
    float phi = acos(2.0f * v - 1.0f);
    float r = pow(randValue(id.x + 15234), 1.0f / 3.0f);
    float sinTheta = sin(theta);
    float cosTheta = cos(theta);
    float sinPhi = sin(phi);
    float cosPhi = cos(phi);

    float x = r * sinPhi * cosTheta;
    float y = r * sinPhi * sinTheta;
    float z = r * cosPhi;

    star.position = float4(x, y, z, 1.0f);
    star.position.xyz *= _Radius;

    float yaw = randValue(star.position.x) * 2.0f * PI;
    float pitch = randValue(star.position.y) * 2.0f * PI;
    float roll = randValue(star.position.z) * 2.0f * PI;

    float4x4 rotationMat = {
        cos(yaw) * cos(pitch), cos(yaw) * sin(pitch) * sin(roll) - sin(yaw) * cos(roll), cos(yaw) * sin(pitch) * cos(roll) + sin(yaw) * sin(roll), 0,
        sin(yaw) * cos(pitch), sin(yaw) * sin(pitch) * sin(roll) + cos(yaw) * cos(roll), sin(yaw) * sin(pitch) * cos(roll) - cos(yaw) * sin(roll), 0,
        -sin(pitch), cos(pitch) * sin(roll), cos(pitch) * cos(roll), 0,
        0, 0, 0, 1
    };

    star.rotation = rotationMat;
    
    _Stars[id.x] = star;
}
